load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@rules_cc//cc:defs.bzl", "cc_binary")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load", "oci_push")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

cc_binary(
    name = "nut_exporter",
    srcs = ["main.cc"],
    visibility = ["//visibility:public"],
    deps = [
        "@abseil-cpp//absl/flags:flag",
        "@abseil-cpp//absl/flags:parse",
        "@abseil-cpp//absl/strings:str_format",
        "@com_github_networkupstools_nut//:nutclient",
        "@prometheus-cpp//pull",
    ],
)

pkg_tar(
    name = "layer",
    srcs = [
        ":nut_exporter",
        "@com_github_networkupstools_nut//:upsd",
        "@com_github_networkupstools_nut//:usbhid-ups",
    ],
    package_dir = "/",
)

oci_image(
    name = "image",
    base = "@debian",
    entrypoint = ["/nut_exporter"],
    target_compatible_with = ["@platforms//os:linux"],
    tars = [
        ":layer",
    ],
)

oci_load(
    name = "image_tarball",
    image = ":image",
    repo_tags = ["nut-exporter:bazel"],
    target_compatible_with = ["@platforms//os:linux"],
)

oci_push(
    name = "image_push",
    image = ":image",
    remote_tags = ["latest"],
    repository = "ghcr.io/kleinpa/nut-exporter",
)

build_test(
    name = "build_test",
    targets = [
        ":nut_exporter",
        ":image",
    ],
)
